buildscript {
    ext {
        encoding = 'UTF-8'
        compatibility = 11
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'antlr'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.jreleaser' version '1.18.0'
}

group = 'io.luchta'
version = '1.8.2'

sourceCompatibility = compatibility
targetCompatibility = compatibility

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src-gen/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

dependencies {
    implementation group: 'org.apache.poi', name: 'poi', version: '5.4.1'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.4.1'

    implementation "jakarta.xml.bind:jakarta.xml.bind-api:4.0.2"
    implementation "com.sun.xml.bind:jaxb-impl:4.0.5"
    
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.19.0'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.19.0'


    antlr "org.antlr:antlr4:4.13.2"

    testImplementation(platform('org.junit:junit-bom:5.11.4'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        shadow(MavenPublication) {
            artifactId = 'forma4j'
            artifact shadowJar
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'forma4j'
                description = 'Excelの読み書きを行うライブラリ'
                url = 'https://github.com/actier-luchta/forma4j'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Actier,inc'
                    }
                }
                scm {
                    connection = 'git@github.com:actier-luchta/forma4j.git'
                    developerConnection = 'git@github.com:actier-luchta/forma4j.git'
                    url = 'https://github.com/actier-luchta/forma4j'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'localStaging'
            url  = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    project {
        name = 'forma4j'
        description = 'Excelの読み書きを行うライブラリ'
        website = 'https://github.com/actier-luchta/forma4j'
        authors = ['Actier,inc']
        license = 'Apache-2.0'
    }
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository("${layout.buildDirectory.get().asFile}/staging-deploy")
                }
            }
        }
    }
}

shadowJar {
    archiveClassifier.set("")
}

tasks.named("publishShadowPublicationToLocalStagingRepository") {
    dependsOn("jar")
    dependsOn("shadowJar", "sourcesJar", "javadocJar")
}

generateGrammarSource {
    outputDirectory = file("src-gen/main/java")
}

tasks.named("sourcesJar").configure {
    dependsOn(":generateGrammarSource")
}
