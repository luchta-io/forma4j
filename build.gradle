buildscript {
    ext {
        encoding = 'UTF-8'
        compatibility = 11
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'signing'
    id 'maven-publish'
    id 'antlr'
}

group = 'io.luchta'
version = '1.3.2'

sourceCompatibility = compatibility
targetCompatibility = compatibility

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src-gen/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

dependencies {
    implementation group: 'org.apache.poi', name: 'poi', version: '5.2.3'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.2.3'

    implementation "jakarta.xml.bind:jakarta.xml.bind-api:4.0.0"
    implementation "com.sun.xml.bind:jaxb-impl:4.0.3"
    
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.15.2'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.15.2'

    antlr "org.antlr:antlr4:4.13.0"

    testImplementation(platform('org.junit:junit-bom:5.9.3'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'forma4j'
            from components.java
            pom {
                name = 'forma4j'
                description = 'Excelの読み書きを行うライブラリ'
                url = 'https://github.com/actier-luchta/forma4j'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Actier,inc'
                    }
                }
                scm {
                    connection = 'git@github.com:actier-luchta/forma4j.git'
                    developerConnection = 'git@github.com:actier-luchta/forma4j.git'
                    url = 'https://github.com/actier-luchta/forma4j'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.hasProperty('sonatypeUsername') ? "${sonatypeUsername}" : ""
                password = project.hasProperty('sonatypePassword') ? "${sonatypePassword}" : ""
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

generateGrammarSource {
    outputDirectory = file("src-gen/main/java")
}
